buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        mavenCentral()
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "B4"
group = "keystrokesmod"
archivesBaseName = "Smn"

sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_20"
    makeObfSourceJar = false
    clientJvmArgs += '-Dfml.coreMods.load=keystrokesmod.mixins.MixinLoader'
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://jitpack.io' }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'commons-codec'
    }
    
    compile 'org.projectlombok:lombok:1.18.22'
    compile 'org.jetbrains:annotations:24.1.0'
    compile 'cn.hutool:hutool-all:5.8.26'
    compile files('libraries/json-20220924.jar')
    compile files('libraries/discord-rpc.jar')

    // Uncomment these if needed
    // compile files('libraries/ViaAprilFools-3.0.2-SNAPSHOT.jar')
    // compile files('libraries/ViaBackwards-5.0.4-SNAPSHOT.jar')
    // compile files('libraries/ViaLegacy-3.0.3-SNAPSHOT.jar')
    // compile files('libraries/ViaLoader-3.0.3-SNAPSHOT.jar')
    // compile files('libraries/ViaRewind-4.0.3-SNAPSHOT.jar')
    // compile files('libraries/ViaVersion-5.0.4-SNAPSHOT.jar')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "mixins.raven.refmap.json"
}

shadowJar {
    configurations = [project.configurations.embed]
    classifier = 'all'
    
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    exclude 'META-INF/MUMFREY.RSA'
    exclude 'META-INF/maven/**'
    exclude 'org/**/*.html'
    
    manifest {
        attributes(
            'MixinConfigs': 'mixins.raven.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'FMLCorePlugin': 'keystrokesmod.mixins.MixinLoader',
            'ForceLoadAsMod': true,
            'FMLCorePluginContainsFMLMod': true,
            'ModSide': 'CLIENT'
        )
    }
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

task renameShadowJar(type: Copy) {
    from("${buildDir}/libs/${archivesBaseName}-${version}-all.jar")
    into("${buildDir}/libs")
    rename { String fileName ->
        fileName.replace('-all.jar', '.jar')
    }
}

tasks.reobfShadowJar.finalizedBy renameShadowJar
tasks.build.dependsOn reobfShadowJar

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
